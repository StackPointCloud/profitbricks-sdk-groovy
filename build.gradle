/*
   Copyright 2018 Profitbricks GmbH

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1',
                  'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0'
    }
}


apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'


archivesBaseName = 'groovy-sdk'
group 'com.profitbricks'
version '3.0-SNAPSHOT'
targetCompatibility = 7
final ppx = 'com.profitbricks.sdk'


repositories {
    mavenCentral()
    jcenter()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}


modifyPom {
    //noinspection GroovyAssignabilityCheck
    project {
        //noinspection GroovyAssignabilityCheck
        name 'Profitbricks Groovy SDK'
        description 'The official Groovy/Java SDK for the Profitbricks Cloud API v4.'
        url 'https://github.com/profitbricks/profitbricks-sdk-groovy'
        inceptionYear '2018'
        scm {
            url 'https://github.com/profitbricks/profitbricks-sdk-groovy/tree/master'
            connection 'scm:git:git://github.com/profitbricks/profitbricks-sdk-groovy.git'
            developerConnection 'scm:git:ssh://github.com:profitbricks/profitbricks-sdk-groovy.git'
        }
        licenses {
            license {
                //noinspection GroovyAssignabilityCheck
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                //noinspection GroovyAssignabilityCheck
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'zerofudge'
                //noinspection GroovyAssignabilityCheck
                name 'Frank Geusch'
                email 'frank.geusch@profitbricks.com'
            }
        }
    }
}


extraArchive {
    sources = true
    tests = true
    javadoc = true
}


nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}


dependencies {
    testCompile 'org.spockframework:spock-core:+'
    runtime     'org.apache.logging.log4j:log4j-jcl:2.+'
    compile     'org.codehaus.groovy:groovy-all:2.4.+',
                'org.codehaus.groovy.modules.http-builder:http-builder:+',
                'org.apache.logging.log4j:log4j-core:2.+'
}


test {
    maxParallelForks = 2

    ['user', 'password', 'URL'].each {
        systemProperty "${ppx}.${it}", System.getProperty("${ppx}.${it}")
    }
    systemProperty "${ppx}.verifySSL", false
}


compileGroovy {
    options.compilerArgs += '-proc:none'
}


//noinspection GroovyAssignabilityCheck
tasks.withType(GroovyCompile) {
    options.compilerArgs += '-proc:none'
}
