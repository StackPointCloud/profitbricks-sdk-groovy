apply plugin: 'groovy'
apply plugin: 'maven-publish'

group 'com.profitbricks'
version '3.0-SNAPSHOT'

targetCompatibility = 8

repositories {
    mavenCentral()
    jcenter()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            //noinspection GroovyAssignabilityCheck
            artifact sourceJar
            //noinspection GroovyAssignabilityCheck
            artifact javadocJar
        }

        mavenCustom(MavenPublication) {
            pom.withXml {
                asNode().appendNode('description', 'The official groovy/java SDK for the Profitbricks Cloud API.')
            }
        }
    }

    repositories {
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
}

dependencies {
    testCompile 'org.spockframework:spock-core:+'
    runtime     'org.apache.logging.log4j:log4j-jcl:2.+'
    compile     'org.codehaus.groovy:groovy-all:2.4.+',
                'org.codehaus.groovy.modules.http-builder:http-builder:+',
                'org.codehaus.gpars:gpars:+',
                'org.apache.logging.log4j:log4j-core:2.+'
}

test {
    maxParallelForks = 2

    ['user', 'password', 'URL'].each {
        systemProperty "api.${it}", System.getProperty("api.${it}")
    }
    systemProperty "api.verifySSL", false
}

compileGroovy {
    options.compilerArgs += '-proc:none'
}


task sourceJar(type: Jar) {
    //noinspection GrUnresolvedAccess, GroovyAssignabilityCheck
    from sourceSets.main.allGroovy
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from groovydoc
    classifier = 'javadoc'
}

//noinspection GroovyAssignabilityCheck
tasks.withType(GroovyCompile) {
    options.compilerArgs += '-proc:none'
}
